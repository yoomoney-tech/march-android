afterEvaluate {
    apply plugin: 'maven-publish'

    archivesBaseName = 'march'
    group = 'ru.yoo.sdk'
    version = getAppVersionName()

    sourceSets {
        main {
            java {}
            kotlin {}
        }
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }

    task javadocJar(type: Jar) {
        from "build/javadoc"
        classifier = 'javadoc'
    }

    publishing {
        publications {
            marchLib(MavenPublication) {
                artifactId = "march"
                pom.packaging 'aar'

                artifact bundleReleaseAar
                artifact(javadocJar)
                artifact(sourcesJar)

                generatePom(pom)
            }
        }

        repositories {
            maven {
                name = "GithubPackages"
                url = uri("https://maven.pkg.github.com/yoomoney-tech/march-android")
                credentials {
                    username = System.getenv('GITHUB_USER') ?: project.properties['GITHUB_USER']
                    password = System.getenv('GITHUB_PERSONAL_ACCESS_TOKEN') ?: project.properties['GITHUB_PERSONAL_ACCESS_TOKEN']
                }
            }
            maven {
                name = 'CustomMavenRepo'
                url = "file://${buildDir}/repo"
            }
        }
    }
}

def generatePom(def pom) {
    pom.withXml {
        // dependencies
        Node dependenciesNode = asNode().appendNode('dependencies')

        //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
        configurations.api.allDependencies.each {
            def dependencyNode = dependenciesNode.appendNode('dependency')
            dependencyNode.appendNode('groupId', it.group)
            dependencyNode.appendNode('artifactId', it.name)
            dependencyNode.appendNode('version', it.version)
        }
    }
}
